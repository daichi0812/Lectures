@echo off

echo -----------------------------------------------------------------------
echo 独立行政法人 情報処理推進機構「暗号技術に関するe-Learning用教材」
echo　　　　　　　　　　　　実習用プログラム
echo               「t1-5.1　RSA暗号アルゴリズムの実習」
echo -----------------------------------------------------------------------

setlocal
set path=..\..\common-programs;%path%

if "%3" == ""   goto USAGE
if "%2" == ""   goto USAGE
if "%1" == "-e" goto ENCRYPTO
if "%1" == "-d" goto DECRYPTO
goto USAGE

rem -----------------------------------------------------------------------
rem 使い方
:USAGE
echo エラー：引数が誤っているか、指定されていません。
echo 使い方：t1-5 -e 公開鍵ファイル名 暗号文ファイル名 (暗号化)
echo 　　　  t1-5 -d 秘密鍵ファイル名 暗号文ファイル名 (復号)
goto QUIT
rem -----------------------------------------------------------------------
rem 暗号化
:ENCRYPTO
echo ・RSA暗号アルゴリズムにより、平文(入力テキスト)を公開鍵で暗号化します。
echo 　(鍵は、%2 を使います。)
echo 　キーボードで平文を１行だけ入力してください。
echo ＊入力し終わったら、エンターキーを押し、続いてCtrl-Z(Ctrlキーと
echo 　Zキーを同時に押す)を入力して、もう１度エンターキーを押してください。
rsa_cipher.exe -e -c -k %2 > %3
type %3
goto QUIT
rem -----------------------------------------------------------------------
rem 復号
:DECRYPTO
echo ・RSA暗号アルゴリズムにより暗号文を秘密鍵で復号します。
echo 　(鍵は、%2 を使います。)
echo 復号の結果は以下の通りです。
type %3 | rsa_cipher.exe -d -b -k %2
goto QUIT
rem -----------------------------------------------------------------------
rem プログラム終了
:QUIT
endlocal
rem -----------------------------------------------------------------------
